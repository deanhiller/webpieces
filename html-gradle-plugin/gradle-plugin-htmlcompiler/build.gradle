plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'checkstyle'
    id 'jacoco' //code coverage
    id 'eclipse'
    id 'idea'
    id 'signing'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.16.0'
}

apply from: '../../config/global.gradle'

dependencies {

    implementation libs.apache.commons.io
    implementation libs.google.guice
    api libs.webpieces.core.util
    api libs.webpieces.template.devlib
	
    testImplementation libs.junit

}

tasks.whenTaskAdded {task ->
    if(task.name.toLowerCase().contains("sonatype")) {
        task.enabled = false
    }
}

gradlePlugin {
    plugins {
        htmlCompilerPlugin {
            id = 'org.webpieces.templatecompiler'
            implementationClass = 'org.webpieces.gradle.compiler.TemplateCompilerPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/deanhiller/webpieces'
    vcsUrl = 'https://github.com/deanhiller/webpieces'
    description = 'Webpieces HTML template compiler'
    tags = ['webpieces']

    plugins {
        htmlCompilerPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Webpieces HTML Compiler'
        }
    }

}

//need this with pluginBundle for duplicate files
processResources {
    duplicatesStrategy = 'include'
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

publish.onlyIf { false }
if(project.hasProperty('projVersion')) {
    publishMavenJavaPublicationToSonatypeRepository.onlyIf {false}
}
