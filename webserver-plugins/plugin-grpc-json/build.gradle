plugins {
    id 'java-library'
    id 'checkstyle'
    id 'jacoco' //code coverage
    id 'eclipse'
    id 'idea'
    id 'signing'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.8.15'
}

apply from: '../../config/global.gradle'

group 'org.webpieces.plugins'

dependencies {

    api deps['grpc-stub']

    implementation deps['http1_1-parser']
    implementation deps['http-router']

    implementation deps['grpc-json']
    implementation deps['grpc-protobuf']

}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.15.6'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.37.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                description = 'Webpieces plugin that converts between GRPC Protobuf and JSON'
            }
        }
    }
}
